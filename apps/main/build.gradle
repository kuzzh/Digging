apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'androidx.navigation.safeargs'

kapt {
    correctErrorTypes = true
}

android {
    dataBinding {
        enabled = true
    }
    compileSdkVersion Versions.compileSDK
    defaultConfig {
        applicationId "com.movies.douqi"
        minSdkVersion Versions.minSDK
        targetSdkVersion Versions.targetSDK
        multiDexEnabled true
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        debug {
            storeFile rootProject.file("buildsystem/app-debug.jks")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }

        release {
            storeFile rootProject.file(propOrDef('DOUQI_RELEASE_STORE_FILE', ""))
            storePassword propOrDef('DOUQI_RELEASE_STORE_PWD', "")
            keyAlias propOrDef('DOUQI_RELEASE_KEY_ALIAS', "")
            keyPassword propOrDef('DOUQI_RELEASE_KEY_PWD', "")
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            versionNameSuffix "-dev"
            applicationIdSuffix ".debug"
        }
        release {
            signingConfig signingConfigs.release
            shrinkResources false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'kotlin/**'
        exclude '**/*.kotlin_metadata'
        exclude 'META-INF/*.kotlin_module'
        exclude 'META-INF/*.version'
    }

    lintOptions {
        // Disable lintVital. Not needed since lint is run on CI
        checkReleaseBuilds false

        disable 'GradleCompatible'
    }

    dynamicFeatures = [
            ":feature_nba",
            ":feature_douban",
            ":feature_dytt",
            ":feature_mahua"
    ]
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    api project(":module_core")
    api project(":module_data")
}

dependencies {
    kapt "com.google.dagger:dagger-compiler:${Versions.dagger}"
    kapt "com.google.dagger:dagger-android-processor:${Versions.dagger}"
    kapt "com.github.bumptech.glide:compiler:${Versions.glide}"
    kapt "androidx.lifecycle:lifecycle-compiler:${Versions.lifecycle}"
    kapt "androidx.room:room-compiler:${Versions.room}"
    kapt "com.airbnb.android:epoxy-processor:${Versions.epoxy}"
}

dependencies {
    implementation "androidx.appcompat:appcompat:${Versions.appcompat}"
    implementation "androidx.constraintlayout:constraintlayout:${Versions.constraint}"
    implementation "com.android.support:multidex:${Versions.multidex}"

    api "android.arch.navigation:navigation-fragment-ktx:${Versions.navigation}"
    api "android.arch.navigation:navigation-ui-ktx:${Versions.navigation}"
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'

    testImplementation "androidx.arch.core:core-testing:${Versions.lifecycle}"
    testImplementation "androidx.room:room-testing:${Versions.room}"
    testImplementation "androidx.paging:paging-common:${Versions.paging}"
}
